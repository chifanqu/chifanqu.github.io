---
layout: post
title: 'python字节码还原'
date: 2020-04-20
author: 吃饭去
cover: '../assert'
tags: reverse
typora-root-url: a\a
typora-copy-images-to: ..\assets\img
---

# python字节码还原

在数字中国赛上遇到了一道python字节码的逆向，之前没有接触过直接看字节码的题目，所以来总结下。

## 0x1 python字节码简介

查看字节码的方式：

```python
import dis
def check():
    return 1
dis.dis(check)
```

也可以直接看py文件的

```bash
python -m dis test.py
```

转化后的字节码格式如下：

```
源码行号  |  指令偏移量  | 指令符号 | 指令参数  |  实际参数值
```

## 0x2 加载变量

### 常数

LOAD_CONST

加载常量，比如数值、字符串等等，一般用于传给函数的参数

```
55       12 LOAD_GLOBAL              1 (test)
         15 LOAD_FAST                0 (2) #读取2
         18 LOAD_CONST               1 ('output')
         21 CALL_FUNCTION            2
test(2, 'output')
```

### 局部变量

LOAD_FAST

加载局部变量的值，也就是读取值，用于计算或者函数调用传参等。

STORE_FAST

保存值到局部变量。

```
61          77 LOAD_FAST                0 (n)
            80 LOAD_FAST                3 (p)
            83 INPLACE_DIVIDE
            84 STORE_FAST               0 (n)
n = n/p
```

### 全局变量

LOAD_GLOB用来加载全局变量，包括指定函数名，类名，模块名等全局符号。

 STORE_GLOB用来给全局变量赋值。

```
8           6 LOAD_CONST               2 (101)
            9 STORE_GLOBAL             0 (global1)
            20 LOAD_GLOBAL              0 (global1)
            23 PRINT_ITEM
global global1
global1 = 101
print global
```

## 0x3 数据结构

### list

BUILD_LIST

创建一个list

```
13           0 LOAD_CONST               1 (1)
             3 LOAD_CONST               2 (2)
             6 BUILD_LIST               2
             9 STORE_FAST               0 (k)
k = [1, 2]
```

### dict

BUILD_MAP

创建一个空的dict

STORE_MAP

初始化dict内容

```
13           0 BUILD_MAP                1
             3 LOAD_CONST               1 (1)
             6 LOAD_CONST               2 ('a')
             9 STORE_MAP
            10 STORE_FAST               0 (k)
k = {'a': 1}
```

修改dict

```
14          13 LOAD_CONST               3 (2)
            16 LOAD_FAST                0 (k)
            19 LOAD_CONST               4 ('b')
            22 STORE_SUBSCR
k['b'] = 2
```

### slice

BUILD_SLIC用于创建slice。对于list、元组、字符串都可以使用slice的方式进行访问。

```
13            0 LOAD_CONST               1 (1)
              3 LOAD_CONST               2 (2)
              6 LOAD_CONST               3 (3)
              9 BUILD_LIST               3
             12 STORE_FAST               0 (k1) //k1 = [1, 2, 3]
 
 14          15 LOAD_CONST               4 (10)
             18 BUILD_LIST               1
             21 LOAD_FAST                0 (k1)
             24 LOAD_CONST               5 (0)
             27 LOAD_CONST               1 (1)
             30 LOAD_CONST               1 (1)
             33 BUILD_SLICE              3
             36 STORE_SUBSCR                    //k1[0:1:1] = [10]
 
 15          37 LOAD_CONST               6 (11)
             40 BUILD_LIST               1
             43 LOAD_FAST                0 (k1)
             46 LOAD_CONST               1 (1)
             49 LOAD_CONST               2 (2)
             52 STORE_SLICE+3                   //k1[1:2] = [11]
 
 16          53 LOAD_FAST                0 (k1)
             56 LOAD_CONST               1 (1)
             59 LOAD_CONST               2 (2)
             62 SLICE+3
             63 STORE_FAST               1 (a)  //a = k1[1:2]
 
 17          66 LOAD_FAST                0 (k1)
             69 LOAD_CONST               5 (0)
             72 LOAD_CONST               1 (1)
             75 LOAD_CONST               1 (1)
             78 BUILD_SLICE              3
             81 BINARY_SUBSCR
             82 STORE_FAST               2 (b) //b = k1[0:1:1]
```

## 0x5 循环

`SETUP_LOOP`用于开始一个循环。`SETUP_LOOP 26 (to 35)`中`35`表示循环退出点。

#### while循环

```
23           0 LOAD_CONST               1 (0)
             3 STORE_FAST               0 (i) // i=0
 
24           6 SETUP_LOOP              26 (to 35)
       >>    9 LOAD_FAST                0 (i) //循环起点
            12 LOAD_CONST               2 (10)
            15 COMPARE_OP               0 (<)
            18 POP_JUMP_IF_FALSE       34     //while i < 10:
 
25          21 LOAD_FAST                0 (i)
            24 LOAD_CONST               3 (1)
            27 INPLACE_ADD                    
            28 STORE_FAST               0 (i) // i += 1
            31 JUMP_ABSOLUTE            9    // 回到循环起点
       >>   34 POP_BLOCK
       >>   35 LOAD_CONST               0 (None)
i = 0
while i < 10:
    i += 1
```

#### for 循环

```
    238 LOAD_FAST                3 (sieve)#sieve是个list
    241 GET_ITER                    //开始迭代sieve
>>  242 FOR_ITER                24 (to 269) //继续iter下一个x
    245 STORE_FAST               4 (x)
    ...
    266 JUMP_ABSOLUTE          242 //循环
    for x in sieve:
```

## 0x6 if

`POP_JUMP_IF_FALSE`和`JUMP_FORWARD`一般用于分支判断跳转。`POP_JUMP_IF_FALSE`表示条件结果为`FALSE`就跳转到目标偏移指令。`JUMP_FORWARD`直接跳转到目标偏移指令。



一个不懂的指令，详见：`https://docs.python.org/zh-cn/3.8/library/dis.html`

